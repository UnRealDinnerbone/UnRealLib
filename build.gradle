buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id 'java'
    id "maven-publish"
}

sourceSets {
    core
}

group 'com.unrealdinnerbone'

def ENV = System.getenv()
def buildNumber = ENV.GITHUB_RUN_NUMBER ? "${ENV.GITHUB_RUN_NUMBER}" : "0"
version = project.getProperty("version") + ("+build." + buildNumber).replace("\\", "")

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.unreal.codes/repository/unreal/" }
    maven { url "https://oss.jfrog.org/artifactory/libs-release" }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
    from sourceSets.core.allJava
}

configurations {
    api
    implementation.extendsFrom api
    apiElements.extendsFrom api
}

dependencies {
    coreCompileOnly 'org.jetbrains:annotations:21.0.1'
    api sourceSets.core.output
    api "org.fusesource.jansi:jansi:2.3.2"
    api 'org.slf4j:slf4j-api:1.7.31'
    api "ch.qos.logback:logback-classic:1.2.3"
    api 'com.google.code.gson:gson:2.8.7'
    api 'org.jetbrains:annotations:21.0.1'
}


task coreJar(type: Jar) {
    archiveClassifier = "core"
    from sourceSets.core.output
}

build {
    dependsOn coreJar
}

jar{
    from sourceSets.core.output
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact coreJar
            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "https://maven.unreal.codes/repository/unreal/"
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }

        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}